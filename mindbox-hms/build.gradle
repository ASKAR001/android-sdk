plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.huawei.agconnect'
    id 'maven'
    id 'signing'
}
apply plugin: 'maven-publish'
group = 'com.github.mindbox-moscow'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    task androidSourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    artifact androidSourcesJar
                    groupId = 'cloud.mindbox'
                    artifactId = 'mindbox-huawei'
                    version = SDK_VERSION_NAME
                    pom {
                        name = 'Mindbox Android SDK'
                        description = 'Test release'
                        url = 'https://github.com/mindbox-moscow/android-sdk'
                        licenses {
                            license {
                                name = 'The Mindbox License'
                                url = 'https://github.com/mindbox-moscow/android-sdk/blob/master/LICENSE.md'
                            }
                        }
                        developers {
                            developer {
                                id = 'mindbox'
                                name = 'Petr Nikitin'
                                email = 'nikitin@mindbox.ru'
                            }
                        }
                        scm {
                            connection = 'scm:https://github.com/mindbox-moscow/android-sdk.git'
                            developerConnection = 'scm:git://github.com/mindbox-moscow/android-sdk.git'
                            url = 'https://github.com/mindbox-moscow/android-sdk'
                        }
                    }
                }
            }
            repositories {
                if (!project.hasProperty("sonatypeUsername")) {
                    ext.sonatypeUsername = "sonatypeUsername"
                }
                if (!project.hasProperty("sonatypePassword")) {
                    ext.sonatypePassword = "sonatypePassword"
                }
                maven {
                    name = "OSSRH"
                    url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2")
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
        signing {
            sign publishing.publications.release
        }
    }
}

dependencies {
    implementation project(path: ':sdk')

    implementation sdkDependencies.kotlinx_coroutines_core
    implementation sdkDependencies.hms_push
    implementation sdkDependencies.hms_ads_identifier
    implementation sdkDependencies.gson
    implementation sdkDependencies.androidx_annotations
}